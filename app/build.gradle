apply from: '../repositories.gradle'
apply plugin: 'com.android.application'
apply from: '../dist.gradle'

ext {
    /* Version code for *next* release, bump *after* a release is created. */
    // 1 or more digits
    versionMajor = 2
    // exactly 1 digit
    versionMinor = 0
    // exactly 2 digits
    versionBuild = 6
}

android {
    compileSdkVersion 24
    buildToolsVersion '23.0.1'

    defaultConfig {
        applicationId "com.afwsamples.testdpc"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode versionMajor * 1000 + versionMinor * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionBuild}"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
            storeFile file("$projectDir/debug.keystore")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }
}

dependencies {
    compile "com.android.support:support-v4:22.0.0"
    compile(name:'setup-wizard-lib-platform-release', ext:'aar')
}

// Temporary workaround to use the current sdk.
android.applicationVariants.all {
    variant -> variant.javaCompile.options.compilerArgs << "-Xbootclasspath/p:${rootDir}/prebuilts/sdk/current/android.jar"
}
