apply plugin: 'com.android.application'

android {
    compileSdkVersion 'android-MNC'
    buildToolsVersion '23.0.0 rc1'

    defaultConfig {
        applicationId "com.google.android.testdpc"
        minSdkVersion 21
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    /* TODO: remove once release version of M sdk is released */
    def minVersion = defaultConfig.getMinSdkVersion().getApiString()
    def targetVersion = defaultConfig.getTargetSdkVersion().getApiString()
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.processManifest.doLast {
                def manifestOutFile = output.processManifest.manifestOutputFile
                def newFileContents = manifestOutFile.getText('UTF-8').
                        replace('android:minSdkVersion="MNC"',
                                'android:minSdkVersion="' + minVersion + '"')
                newFileContents = newFileContents.replace('android:targetSdkVersion="MNC"',
                        'android:targetSdkVersion="' + targetVersion + '"')
                manifestOutFile.write(newFileContents, 'UTF-8')
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
